01 suma srazek = 

-- scenar1: Respektuji filtry vizualu, zadne nerusim, nepridavam. Pouzivam zakladni agregacni funkce
-- typu SUM(),MIN()
SUM(data[srazky])

__________________________

02 suma srazek ruzyne = 
-- suma srazek v danem roce v lokalite ruzyne

-- scenar2: Respektuji filtry vizualu a nastavuji dalsi, tj. pouziji kombinaci funkci
-- SUMX(FILTER()), MINX(FILTER()), MAXX(FILTER()) apod.

SUMX(
    --krok3: Funkce FILTER() vraci tabulku
    FILTER(
    --krok1: Funkce FILTER() prebira tabulku data se vsemi jejimi filtry
        data,
        --krok2: Funkce FILTER() nastavuje filtr na sloupec lokalita
        data[lokalita] = "RUZYNE"),
    --krok4: Funkce SUMX() agreguje sloupec srazky tabulky vracene funkci FILTER()
    data[srazky])
    
    ___________________________________
    
    03 suma razek vsechny roky = 

-- scenar3: Rusim filtry vizualu, zadne nenastavuji, tj. pouziji kombinaci funkci
-- SUMX(ALL()),MINX(ALL()) apod.

SUMX(
    --krok1: Funkce ALL() vytvari kopii tabulky data se vsemi jejimi hodnotami,
    -- tj. tabulka neni filtrovana
    ALL(data),
--krok2: Funkce SUMX() scita sloupec srazky tabulky vracene funkci ALL()
data[srazky])

_________________________________________________


04 suma srazek rok 2000 = 

-- scenar4: Rusim filtry vizualu a nastavuji si vlastni, pouziji kombinaci funkci
-- SUMX(FILTER(ALL())), MINX(FILTER(ALL())) apod.

SUMX(
    --krok3: Funkce FILTER() vraci tabulku
    FILTER(
    -- krok1: Funkce ALL() vytvari kopii tabulky data zbavenou vsech filtru
        ALL(data),
    --krok2: Funkce FILTER() nastavuje filtr na sloupec rok tabulky vracene funkci ALL()
    data[rok] = 2000),
--krok4: Funkce SUMX() agreguje sloupec srazky tabulky vracene funkci FILTER()
data[srazky])

_______________________________

05 suma srazek 12 mesic = 

SUMX(FILTER(data,data[mesic]=12),data[srazky])

___________________________________________

06 pocet dnu v ruzyni kdy v danem roce neprselo = 

COUNTX(FILTER(data,data[lokalita] = "RUZYNE" && data[srazky] = 0),data[srazky])

___________________________________________________________________


07 prvni datum maximalni teploty v lokalite ruzyne v danem roce = 
MINX(FILTER(data,data[lokalita] =  "RUZYNE" && data[maximalni_teplota] =
MAXX(FILTER(data,data[lokalita] = "RUZYNE"),data[maximalni_teplota])),data[datum])

_________________________________________________________________

08 suma srazek predchozi rok = 
VAR rok = max(data[rok]) - 1
return
SUMX(FILTER(ALL(data),data[rok] = rok),data[srazky])

__________________________________________

09 maximalni teplota lokality v roce 2000 = 
-- maximalni teplota je ve sloupci maximalni_teplota, ze ktereho dale pocitame maximum
MAXX(FILTER(data,data[rok] = 2000),data[maximalni_teplota])

_____________________________________________

10 maximalni teplotu v dane lokalite v poslednim roce dane lokality = 
MAXX(FILTER(data,data[rok] = max(data[rok])),data[maximalni_teplota])


_________________________________________________

11 suma srazek lucka = 
-- suma srazek v danem roce ve vybrane lokalite
SUM(data[srazky])
-- PLUS
+
-- suma srazek v danem roce za vsechny lokality
SUMX(
    FILTER(
    ALL(data),
    data[rok] = MAX(data[rok])),
data[srazky])

_______________________________________________

17 suma srazek mezirocne = 
-- suma srazek v danem roce
SUM(data[srazky])
-- MÍNUS
-
-- suma srazek v predchozim roce
SUMX(FILTER(all(data),data[rok] = SELECTEDVALUE(data[rok])-1
),data[srazky])

_________________________________________________

18 suma srazek mezirocne ruzyne = 

-- suma srazek v ruzyni v danem roce
SUMX(FILTER(data,data[lokalita] = "ruzyne"),data[srazky])
-- MÍNUS   
-
-- suma srazek v ruzyni v predchozim roce
SUMX(FILTER(ALL(data),data[rok] = SELECTEDVALUE(data[rok])-1 
&& data[lokalita] = "RUZYNE"),data[srazky])

____________________________________________________

24 pf barva tabulky = 

-- maximalni teplota v danem mesici,roce,lokalite
VAR max_teplota = MAX(data[maximalni_teplota])

-- maximalni teplota v roce, lokalite

VAR max_teplota_vsechny_roky =
MAXX(FILTER(ALL(data),data[mesic] = SELECTEDVALUE(data[mesic]) && data[lokalita] = SELECTEDVALUE(data[lokalita])),data[maximalni_teplota])

RETURN
IF(max_teplota = max_teplota_vsechny_roky,"tomato")
